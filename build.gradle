allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'signing'

  task allDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    source(fileTree(dir: 'build/classes/main', exclude: '**/*.class'))
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath
  }

  task javadocJar(type: Jar, dependsOn: allDocs) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
  }

  task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    from fileTree(dir: 'build/classes/main', exclude: '**/*.class')
    classifier = 'sources'
  }

  compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }
  group = 'uk.kludje'
  version = '0.3-SNAPSHOT'

  artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
  }

  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        //TODO: gradle + release profiles
        //snapshots: https://oss.sonatype.org/content/repositories/snapshots
        //releases: https://oss.sonatype.org/service/local/staging/deploy/maven2
        repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          packaging 'jar'
          url 'https://github.com/mcdiae/kludje'

          scm {
            url 'git@github.com:mcdiae/kludje.git'
            connection 'git@github.com:mcdiae/kludje.git'
            developerConnection 'git@github.com:mcdiae/kludje.git'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'mcdiae'
              name 'McDowell'
            }
          }
        }
      }
    }
  }
}

subprojects {
  repositories {
    mavenCentral()
  }
  dependencies {
    testCompile 'junit:junit:4.10'
    testCompile 'com.google.jimfs:jimfs:1.0-rc3'
  }
}

project(':kludje-testkit') {
  dependencies {
    compile project(':kludje-annotation')
    compile project(':kludje-core')
    compile project(':kludje-proxy')
  }
}

project(':kludje-core') {
  dependencies {
    compile project(':kludje-annotation')
  }
}

project(':kludje-proxy') {
  dependencies {
    compile project(':kludje-core')
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name 'kludje'
        packaging 'pom'
        description 'Java 8 lambda API'
      }
    }
  }
}