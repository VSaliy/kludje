allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'signing'

  task allDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    source(fileTree(dir: 'build/classes/main', exclude: '**/*.class'))
  }

  task javadocJar(type: Jar, dependsOn: allDocs) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
  }

  task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    from fileTree(dir: 'build/classes/main', exclude: '**/*.class')
    classifier = 'sources'
  }

  compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }
  group = 'uk.kludje'
  version = '0.1-SNAPSHOT'

  artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
  }
}

subprojects {
  repositories {
    mavenCentral()
  }
  dependencies {
    testCompile 'junit:junit:4.10'
    testCompile 'com.google.jimfs:jimfs:1.0-rc3'
  }
}

project(':kludje-testkit') {
  dependencies {
    compile project(':kludje-annotation')
    compile project(':kludje-core')
  }
}

project(':kludje-core') {
  dependencies {
    compile project(':kludje-annotation')
  }
}


sourceSets.each { println it.name }
